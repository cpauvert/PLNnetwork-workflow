# Load configuration file (.yaml)
# Default is the test data trichoptera from R package PLNmodels
configfile: "../config/config.yaml"

rule pln_prepare_data:
    input:
        counts=config["counts"],
        covariates=config["covariates"]
    output:
        "results/PLN/prepared_data.RDS"
    # Optional arguments for `prepare_data()`
    # Please provide them as python `key=value` pairs
    #params:
    #    offset="TSS"
    log:
        "results/logs/prepare_data.log"
    conda:
        "envs/PLNnetwork.yaml"
    script:
        "scripts/prepare_data.R"

rule pln_infer_networks:
    input:
        "results/PLN/prepared_data.RDS"
    output:
        models="results/PLN/networks.RDS",
        plot=report("results/PLN/plot_models.png",
         caption="Criteria evolution for network model fits")
    # Optional arguments for ``PLNnetwork()``
    # Please provide them as python ``key=value`` pairs
    params:
        # The formula, as character here, is interpreted by R
        formula=config["formula"]
    log:
        "results/logs/infer_networks.log"
    conda:
        "envs/PLNnetwork.yaml"
    threads: 1 # set desired number of threads here
    script:
        "scripts/infer_networks.R"
