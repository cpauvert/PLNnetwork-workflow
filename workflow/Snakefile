# Load configuration file (.yaml)
# Default is the test data trichoptera from R package PLNmodels
configfile: "../config/config.yaml"

rule all:
    input:
        expand("results/PLN/plots/plot_network_{criteria}.png", criteria=config["criteria"])

rule pln_prepare_data:
    input:
        counts=config["counts"],
        covariates=config["covariates"]
    output:
        "results/PLN/RDS/prepared_data.RDS"
    # Optional arguments for `prepare_data()`
    # Please provide them as python `key=value` pairs
    #params:
    #    offset="TSS"
    log:
        "results/logs/prepare_data.log"
    conda:
        "envs/PLNnetwork.yaml"
    script:
        "scripts/prepare_data.R"

rule pln_infer_networks:
    input:
        "results/PLN/RDS/prepared_data.RDS"
    output:
        models="results/PLN/RDS/networks.RDS",
        plot=report("results/PLN/plots/plot_models.png",
         caption="Criteria evolution for network model fits")
    # Optional arguments for ``PLNnetwork()``
    # Please provide them as python ``key=value`` pairs
    params:
        # The formula, as character here, is interpreted by R
        formula=config["formula"]
    log:
        "results/logs/infer_networks.log"
    conda:
        "envs/PLNnetwork.yaml"
    threads: 1 # set desired number of threads here
    script:
        "scripts/infer_networks.R"

rule pln_stability_selection:
# Dedicated rule for stability selection to be adapt to threads number
    input:
        "results/PLN/RDS/networks.RDS"
    output:
        models="results/PLN/RDS/networks_stars.RDS",
        plot=report("results/PLN/plots/plot_stars.png",
         caption="Stability path for network model fits")
    # Optional arguments for ``stability_selection()``
    # Please provide them as python ``key=value`` pairs
    #params:
    log:
        "results/logs/stability_selection.log"
    conda:
        "envs/PLNnetwork.yaml"
    threads: 1 # set desired number of threads here
    script:
        "scripts/stability_selection.R"

rule pln_pick_network:
    input:
        expand("results/PLN/RDS/networks{suffix}.RDS",
                suffix="_stars" if "StARS" in config["criteria"] else "")
    output:
        pln="results/PLN/RDS/network_{criteria}.RDS",
        igraph="results/PLN/RDS/igraph_{criteria}.RDS"
    # Optional arguments for ``getBestModel()``
    # Please provide them as python ``key=value`` pairs
    #params:
    #    stability=0.95
    log:
        "results/logs/pick_network_{criteria}.log"
    conda:
        "envs/PLNnetwork.yaml"
    threads: 1 # set desired number of threads here
    script:
        "scripts/pick_network.R"

rule pln_plot_network:
    input:
       "results/PLN/RDS/igraph_{criteria}.RDS"
    output:
        report("results/PLN/plots/plot_network_{criteria}.png",
         caption="Network inferred by ``PLNnetwork`` and selected by {criteria}.")
    log:
        "results/logs/plot_network_{criteria}.log"
    conda:
        "envs/ggraph.yaml"
    threads: 1 # set desired number of threads here
    script:
        "scripts/plot_network.R"
